import com.modrinth.minotaur.TaskModrinthUpload
import com.modrinth.minotaur.dependencies.ModDependency
import com.modrinth.minotaur.dependencies.VersionDependency
import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import mekanism.MergeJars
import net.darkhax.curseforgegradle.TaskPublishCurseForge
import net.darkhax.curseforgegradle.UploadArtifact
import net.neoforged.gradle.dsl.common.runs.run.Run

import java.util.function.Consumer

plugins {
    id 'org.ajoberstar.grgit.service' version '5.2.2'
    id 'net.darkhax.curseforgegradle' version '1.1.23'
    id "com.modrinth.minotaur" version "2.+"
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.133'//https://projects.neoforged.net/neoforged/neogradle
}

tasks.named('wrapper', Wrapper).configure {
    //Define wrapper values here so as to not have to always do so when updating gradlew.properties
    gradleVersion = '8.7'
    distributionType = Wrapper.DistributionType.ALL
}

defaultTasks 'build'

idea {
    module {
        //Exclude directories from being managed
        for (String excludeDirName in ["runs", "out", "logs", "gradle"]) {
            excludeDirs.add(new File(projectDir, excludeDirName))
        }
        //Tell IDEA to always download sources/javadoc artifacts from maven.
        downloadJavadoc = true
        downloadSources = true
    }
}

ext {
    versionProperties = ["version"    : mod_version, "mc_version": minecraft_version_range, "forge_version": forge_version_range, "loader_version": loader_version_range]
    jsonPatterns = ["**/*.json", "**/*.mcmeta"]
    secondaryModules = ['additions', 'defense', 'generators', 'tools']
    extraTypes = ['datagen']
}

sourceSets {
    api {
        //The API has no resources
        resources.srcDirs = []
    }
    main {
        resources {
            include '**/**'
            // copy everything else, but blender and bbmodel files.
            exclude '**/*.blend'
            exclude '**/*.bbmodel'
            //Add the generated main module resources
            srcDirs += ['src/datagen/generated/mekanism']
            //But exclude the cache of the generated data from what gets built
            exclude '.cache'
        }
        compileClasspath += api.output
        runtimeClasspath += api.output
    }
    test {
        //The test module has no resources
        resources.srcDirs = []
        //Add the api to the output, all other ones that need to will get added via setupExtraSourceSets
        compileClasspath += api.output
        runtimeClasspath += api.output
    }
    gameTest {
        runs {
            modIdentifier = "mekanismtests"
        }
        compileClasspath += api.output
        runtimeClasspath += api.output
    }
}

configurations {
    // Sets up a dependency configuration called 'localRuntime'.
    // This configuration should be used instead of 'runtimeOnly' to declare
    // a dependency that will be present for runtime testing but that is
    // "optional", meaning it will not be pulled by dependents of this mod.
    runtimeClasspath.extendsFrom localRuntime

    datagenNonMod
}

//Add all extra source sets that the main sourceSet should have
setupExtraSourceSets(sourceSets.main)
setupExtraSourceSets(sourceSets.gameTest, false)

configurations {
    //Make sure all our sub source set stuff extends the proper base methods so that
    // they can see all the dependencies we have in dependencies including forge
    extendConfigurations(implementation, apiImplementation, testImplementation)
    extendConfigurations(compileOnly, apiCompileOnly, testCompileOnly)
    extendConfigurations(runtimeOnly, apiRuntimeOnly)
}

//Create sourceSets and configurations for each of the additional modules in src/$name and adds a reference to
// the corresponding data gen's resource directory excluding the cache. It also adds the api and main mekanism
// module to the dependencies of the source set we are setting up, and sets up all extra source sets that are
// based on the primary added source set
for (String name : secondaryModules) {
    def sourceSet = sourceSets.create(name)
    sourceSet.runs {
        modIdentifier = name
    }
    sourceSet.resources {
        //Add the generated module resources
        srcDirs += ["src/datagen/generated/mekanism${name}"]
        //But exclude the cache of the generated data from what gets built
        exclude '.cache'
    }
    sourceSet.compileClasspath += sourceSets.api.output
    sourceSet.compileClasspath += sourceSets.main.output
    //Create all secondary sourceSets for this module
    setupExtraSourceSets(sourceSet)
}

//Setup the UPDATE_SOURCESET property in case we are doing any remappings
project.ext."UPDATE_SOURCESETS" = project.sourceSets.collect { it.name }.join(';')

def setupExtraSourceSets(SourceSet base, boolean includeExtra = true) {
    //Expose the base module to junit
    project.sourceSets.test.compileClasspath += base.output
    project.sourceSets.test.runtimeClasspath += base.output
    if (base != project.sourceSets.gameTest) {
        project.sourceSets.gameTest.compileClasspath += base.output
        project.sourceSets.gameTest.runtimeClasspath += base.output
    }
    //Setup and extend configurations for alternate modules. First by making the implementation, compileOnly, runtimeOnly equivalents
    // for those modules extend the main ones
    def baseImplementation = project.configurations.maybeCreate(base.getTaskName(null, "implementation"))
    def baseCompileOnly = project.configurations.maybeCreate(base.getTaskName(null, "compileOnly"))
    def baseRuntimeOnly = project.configurations.maybeCreate(base.getTaskName(null, "runtimeOnly"))
    if (base != project.sourceSets.main) {
        // If this is a secondary module then make the base tasks extend the builtin ones
        baseImplementation.extendsFrom(project.configurations.getByName("implementation"))
        baseCompileOnly.extendsFrom(project.configurations.getByName("compileOnly"))
        baseRuntimeOnly.extendsFrom(project.configurations.getByName("runtimeOnly"))
    }
    if (includeExtra) {
        //And then setup and have all the extra sourceSets have their configurations extend the ones for the base module so that they can
        // properly access the dependency
        for (String extraType : extraTypes) {
            //Setup a source set in extraType/$name
            def extraSourceSet = setupExtraSourceSet(base, extraType)
            //And then setup the configurations for it
            def implExtends = [baseImplementation]
            if (extraType == 'datagen') {
                implExtends.add(project.configurations.getByName("datagenNonMod"))
            }
            project.configurations.maybeCreate(extraSourceSet.getTaskName(null, "implementation")).extendsFrom(*implExtends)
            project.configurations.maybeCreate(extraSourceSet.getTaskName(null, "compileOnly")).extendsFrom(baseCompileOnly)
            project.configurations.maybeCreate(extraSourceSet.getTaskName(null, "runtimeOnly")).extendsFrom(baseRuntimeOnly)
        }
    }
}

SourceSet setupExtraSourceSet(SourceSet baseSourceSet, String extra) {
    def name = baseSourceSet.getName()
    def extraSourceSet = sourceSets.create(baseSourceSet.getTaskName(extra, null))
    extraSourceSet.java.srcDirs = ["src/${extra}/${name}/java"]
    //Resources folder for if we have anything get created by our annotation processors or in the case of game tests, for any nbt presets
    extraSourceSet.resources.srcDirs = ["src/${extra}/${name}/resources"]
    extraSourceSet.compileClasspath += project.sourceSets.api.output
    extraSourceSet.compileClasspath += project.sourceSets.main.output
    if (baseSourceSet != project.sourceSets.main) {
        //If the base sourceSet is main it already is the extra source set and has a reference to the base one from before this if statement
        extraSourceSet.compileClasspath += getExtraSourceSet(project.sourceSets.main, extra).output
        extraSourceSet.compileClasspath += baseSourceSet.output
        extraSourceSet.runs {
            modIdentifier = name
        }
    }
    return extraSourceSet
}

static void extendConfigurations(Configuration base, Configuration... configurations) {
    for (def configuration : configurations) {
        configuration.extendsFrom(base)
    }
}

SourceSet getExtraSourceSet(String base, String name) {
    return getExtraSourceSet(project.sourceSets.getByName(base), name)
}

SourceSet getExtraSourceSet(SourceSet base, String name) {
    return project.sourceSets.getByName(base.getTaskName(name, null))
}

setupTasks(sourceSets.main)
for (String name : secondaryModules) {
    setupTasks(sourceSets.getByName(name))
}

def setupTasks(SourceSet sourceSet) {
    def output = sourceSet.name
    def compileTask = tasks.named(sourceSet.getCompileJavaTaskName(), JavaCompile)
    def replaceResourceTask = tasks.register(sourceSet.getTaskName("replace", "resources"), Copy, {
        setGroup("replace resources")
        outputs.upToDateWhen { false }
        def modsToml = copySpec {
            from(sourceSet.resources) {
                include "META-INF/neoforge.mods.toml"
                expand versionProperties
            }
        }
        //Copy it into the build dir
        with(modsToml)
        into layout.buildDirectory.dir("resources/${output}/")
        //If IntelliJ's output dir exists, copy it there as well
        def intellijPath = sourceSet == project.sourceSets.main ? "production" : output
        if (new File("$rootDir/out/${intellijPath}/resources/").exists()) {
            copy {
                with(modsToml)
                into "$rootDir/out/${intellijPath}/resources/"
            }
        }
        //If Eclipse's output dir exists, copy it there as well
        if (new File("$rootDir/bin/${output}/").exists()) {
            copy {
                with(modsToml)
                into "$rootDir/bin/${output}/"
            }
        }
    })
    //Set the various variables/settings for the different process resources tasks
    tasks.named(sourceSet.getProcessResourcesTaskName(), ProcessResources).configure { ProcessResources process ->
        setGroup("process resources")
        process.duplicatesStrategy(DuplicatesStrategy.FAIL)
        process.exclude('META-INF/neoforge.mods.toml')
        process.from("${projectDir}") { include 'logo.png' }
        //Depend on the compile task so that we can map the computer methods as needed
        dependsOn(compileTask)
        finalizedBy(replaceResourceTask)
        doLast {
            fileTree(dir: getOutputs().getFiles().getAsPath(), includes: jsonPatterns).each {
                File file -> file.setText(JsonOutput.toJson(new JsonSlurper().parse(file)))
            }
        }
    }
    tasks.named(sourceSet.getCompileJavaTaskName(), JavaCompile).configure { setGroup("compile") }
    for (String extraType : extraTypes) {
        def extraSourceSet = getExtraSourceSet(sourceSet, extraType)
        tasks.named(extraSourceSet.getProcessResourcesTaskName(), ProcessResources).configure {
            setGroup("process resources")
            dependsOn(compileTask)
        }
        tasks.named(extraSourceSet.getCompileJavaTaskName(), JavaCompile).configure { setGroup("compile") }
    }
    //Make the various classes tasks depend on the corresponding replaceResources tasks in addition to the default processResources tasks they depend on
    tasks.named(sourceSet.getClassesTaskName()).configure { dependsOn(replaceResourceTask) }
    //Configure specific compile tasks to have the proper annotation processor info
    compileTask.configure { JavaCompile task ->
        setGroup("compile")
        task.options.annotationProcessorPath = configurations.annotationProcessor
        task.options.compilerArgs.addAll([
                "-AmekanismModule=mekanism${sourceSet == sourceSets.main ? "" : output}",//annotation processor param
                "-parameters"
        ])
    }
}

group = "mekanism"
def basicVersion = "${mod_version}." + (System.env.BUILD_NUMBER ?: "homebaked")
version = "${minecraft_version}-${basicVersion}"
project.base.archivesName = "Mekanism"
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of("${java_version}"))
        //TODO - 1.20.5: Figure out why jetbrains jdk isn't found by foojay when requesting java 21 even though they have a release for JDK 21
        //vendor.set(JvmVendorSpec.JETBRAINS)
    }
    withSourcesJar()
}

minecraft {
    accessTransformers {
        files(
                file('src/main/resources/META-INF/accesstransformer.cfg'),
                file('src/additions/resources/META-INF/accesstransformer.cfg'),
                //Dev time only ATs so the file name doesn't have to match accesstransformer.cfg
                file('src/datagen/main/resources/META-INF/datagen_ats.cfg'),
                file('src/gameTest/resources/META-INF/gametest_ats.cfg')
        )
    }
}

static void setupClientAcc(Run run) {
    //The below if statements are to add args to your gradle.properties file in user home
    // (DO NOT add them directly to the gradle.properties file for this project)
    // Setting the below properties allows use of your normal Minecraft account in the
    // dev environment including having your skin load. Each property also has a comment
    // explaining what information to set the value to/format it expects
    // One thing to note is because of the caching that goes on, after changing these
    // variables, you need to refresh the project and rerun genIntellijRuns/genEclipseRuns
    if (run.project.hasProperty('mc_uuid')) {
        //Your uuid without any dashes in the middle
        run.programArguments('--uuid', (String) run.project.property('mc_uuid'))
    }
    if (run.project.hasProperty('mc_username')) {
        //Your username/display name, this is the name that shows up in chat
        // Note: This is not your email, even if you have a Mojang account
        run.programArguments('--username', (String) run.project.property('mc_username'))
    }
    if (run.project.hasProperty('mc_accessToken')) {
        //Your access token, you can find it in your '.minecraft/launcher_accounts.json' file
        run.programArguments('--accessToken', (String) run.project.property('mc_accessToken'))
    }
}

runs {
    configureEach { Run run ->
        if (run.project.hasProperty('forge_force_ansi')) {
            //Force ansi if declared as a gradle variable, as the auto detection doesn't detect IntelliJ properly
            // or eclipse's plugin that adds support for ansi escape in console
            run.jvmArgument("-Dterminal.ansi=${run.project.property('forge_force_ansi')}")
        }

        run.modSources(sourceSets.main, sourceSets.api)

        for (String name : secondaryModules) {
            run.modSource(sourceSets.getByName(name))
        }

        //if the selected toolchain is a JBR, enable DCEVM
        if (run.project.javaToolchains.launcherFor(java.toolchain).map { it.metadata.vendor }.getOrElse("").contains("JetBrains")) {
            run.jvmArgument("-XX:+AllowEnhancedClassRedefinition")
        }
    }
    client { run -> setupClientAcc(run) }
    clientAlt {
        configure("client")
        if (!project.hasProperty('mc_username')) {
            //If the property is missing (so the normal runClient task would use Dev as the name),
            // change the name of the alt client type so that it doesn't conflict with the main dev one
            programArguments('--username', 'AltDev')
        }
    }
    server {
    }
    gameTestServer {
        modSource(sourceSets.gameTest)
    }
    gameTestClient { run ->
        configure("client")
        setupClientAcc(run)
        modSource(sourceSets.gameTest)
    }
    data {
        programArguments('--all', '--output', project.file('src/datagen/generated/').getAbsolutePath(),
                '--mod', 'mekanism', '--existing', project.file('src/main/resources/').getAbsolutePath())

        modSource(sourceSets.datagenMain)

        for (String name : secondaryModules) {
            def modName = "mekanism${name}"
            modSource(getExtraSourceSet(name, 'datagen'))
            programArguments('--mod', modName, '--existing', project.file("src/${name}/resources/").getAbsolutePath())
        }

        dependencies {
            runtime project.configurations.datagenNonMod
        }
    }
}

static void exclusiveRepo(RepositoryHandler handler, String url, String... groups) {
    exclusiveRepo(handler, url, filter -> {
        for (def group : groups) {
            filter.includeGroup(group)
        }
    })
}

static void exclusiveRepo(RepositoryHandler handler, String url, Consumer<InclusiveRepositoryContentDescriptor> filterSetup) {
    handler.exclusiveContent {
        it.forRepositories(handler.maven {
            setUrl(url)
        })
        it.filter { f -> filterSetup.accept(f) }
    }
}

repositories { RepositoryHandler handler ->
    exclusiveRepo(handler, 'https://maven.blamejared.com', filter -> {
        filter.includeGroupByRegex 'com\\.blamejared.*'
        filter.includeGroup 'mezz.jei'
        filter.includeGroup 'org.openzen.zencode'
    })
    exclusiveRepo(handler, 'https://maven.terraformersmc.com/', 'dev.emi')
    exclusiveRepo(handler, 'https://maven.theillusivec4.top/', 'top.theillusivec4.curios')
    exclusiveRepo(handler, 'https://maven.tterrag.com/', 'team.chisel.ctm')
    exclusiveRepo(handler, 'https://squiddev.cc/maven/', 'cc.tweaked', 'org.squiddev')
    exclusiveRepo(handler, 'https://dogforce-games.com/maven/', 'dev.gigaherz.jsonthings')
    exclusiveRepo(handler, 'https://maven2.bai.lol', 'lol.bai', 'mcp.mobius.waila')//WTHIT
    exclusiveRepo(handler, 'https://www.cursemaven.com', 'curse.maven')
    exclusiveRepo(handler, 'https://modmaven.dev/', 'appeng', 'mcjty.theoneprobe')
    exclusiveRepo(handler, 'https://maven.minecraftforge.net/releases/', 'com.github.glitchfiend')
    exclusiveRepo(handler, 'https://maven.thiakil.com', 'com.thiakil', 'moze_intel')
    exclusiveRepo(handler, 'https://maven.parchmentmc.org/', 'org.parchmentmc.data')
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation "net.neoforged:neoforge:${forge_version}"

    compileOnly project(":annotation-processor")
    localRuntime project(":annotation-processor")
    annotationProcessor project(":annotation-processor")

    gameTestImplementation "net.neoforged:testframework:${forge_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    //We use https://github.com/quicktheories/QuickTheories to allow for implementing property based testing
    testImplementation "org.quicktheories:quicktheories:${quicktheories_version}"

    compileOnly("mezz.jei:jei-${previous_minor_minecraft_version}-common-api:${jei_version}")
    compileOnly("mezz.jei:jei-${previous_minor_minecraft_version}-neoforge-api:${jei_version}")
    if (recipe_viewer == "jei" || recipe_viewer == "hybrid") {
        //TODO - 1.20.5: Re-enable when JEI updates
        //localRuntime("mezz.jei:jei-${previous_minor_minecraft_version}-neoforge:${jei_version}")
    }

    compileOnly("dev.emi:emi-neoforge:${emi_version}+${minecraft_version}:api")
    def emi = "dev.emi:emi-neoforge:${emi_version}+${minecraft_version}"
    //TODO - 1.20.5: Re-evaluate
    //Compile against full emi in datagen
    datagenMainCompileOnly(emi)
    if (recipe_viewer == "emi" || recipe_viewer == "hybrid") {
        localRuntime(emi)
    } else {
        //We still need it to load in datagen regardless of if we are using emi or a different viewer so that we can access EMI related objects
        datagenMainRuntimeOnly(emi)
    }

    //TODO - 1.19: Re-enable once https://github.com/Chisel-Team/ConnectedTexturesMod/pull/204 is merged
    //localRuntime("team.chisel.ctm:CTM:${minecraft_version}-${ctm_version}")

    //TODO: Remove having to specify these as non transitive once https://github.com/McJtyMods/TheOneProbe/issues/548 is fixed
    compileOnly("mcjty.theoneprobe:theoneprobe:${top_version}:api") {
        transitive = false
    }
    //TODO - 1.20.4: Re-enable once it doesn't cause connection issues on servers
    /*localRuntime("mcjty.theoneprobe:theoneprobe:${top_version}") {
        transitive = false
    }*/
    compileOnly("curse.maven:jade-api-324717:${jade_api_id}")
    //TODO - 1.20.5: Re-enable when Jade updates
    //localRuntime("curse.maven:jade-324717:${jade_id}")
    compileOnly("mcp.mobius.waila:wthit-api:neo-${wthit_version}")

    def crt = "com.blamejared.crafttweaker:CraftTweaker-neoforge-${previous_minor_minecraft_version}:${crafttweaker_version}"
    compileOnly(crt)
    //TODO - 1.20.5: Re-enable when it updates
    //localRuntime(crt)
    //TODO - 1.20.2: move back to implementation
    def jeiTweaker = "com.blamejared.jeitweaker:JeiTweaker-forge-${previous_minecraft_version}:${jeitweaker_version}"
    compileOnly(jeiTweaker)
    //TODO - 1.20.5: Re-enable when it updates
    //localRuntime(jeiTweaker)

    compileOnly("dev.gigaherz.jsonthings:JsonThings-${previous_minor_minecraft_version}:${json_things_version}")

    //Mods we have dependencies on but don't bother loading into the dev environment
    //compileOnly("curse.maven:projecte-api-226410:${projecte_api_id}")
    compileOnly("moze_intel:ProjectE-api:1.0.1")
    compileOnly("curse.maven:flux-networks-248020:${flux_networks_id}")
    compileOnly("top.theillusivec4.curios:curios-neoforge:${curios_version}:api")
    compileOnly("com.blamejared.recipestages:RecipeStages:${recipe_stages_version}")
    compileOnly("curse.maven:opencomputers2-437654:${oc2_id}")
    compileOnly("cc.tweaked:cc-tweaked-1.20.5-forge-api:${cc_tweaked_version}")
    //localRuntime("cc.tweaked:cc-tweaked-${minecraft_version}-forge:${cc_tweaked_version}")
    compileOnly("curse.maven:female-gender-forge-481655:${wildfire_gender_mod_id}")

    //Dependencies for data generators for mod compat reference
    datagenMainImplementation("appeng:appliedenergistics2-neoforge:${ae2_version}")
    datagenMainRuntimeOnly("com.github.glitchfiend:GlitchCore-neoforge:${minecraft_version}-${glitchcore_version}")
    datagenMainRuntimeOnly("com.github.glitchfiend:TerraBlender-neoforge:${minecraft_version}-${terrablender_version}")
    datagenMainImplementation("com.github.glitchfiend:BiomesOPlenty-neoforge:${minecraft_version}-${biomesoplenty_version}")
    //TODO - 1.20.2: Switch back to datagenMainImplementation
    datagenMainCompileOnly("curse.maven:farmers-delight-398521:${farmers_delight_id}")
    //TODO - 1.20.2: Re-enable after projecte is updated
    //datagenMainRuntimeOnly("curse.maven:projecte-226410:${projecte_id}")

    datagenNonMod "com.thiakil:yaml-ops:${yamlops_version}"
}

def getManifestAttributes(String title) {
    return [
            "Specification-Title"     : title,
            "Specification-Vendor"    : "Mekanism",
            "Specification-Version"   : "${project.mod_version}",
            "Implementation-Title"    : title,
            "Implementation-Version"  : "${project.mod_version}",
            "Implementation-Vendor"   : "Mekanism",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Automatic-Module-Name"   : title.toLowerCase(Locale.ROOT)
    ]
}

tasks.named('jar', Jar).configure { Jar jar ->
    jar.duplicatesStrategy(DuplicatesStrategy.FAIL)
    jar.from([sourceSets.api.output, sourceSets.main.output])
    jar.exclude('crafttweaker_parameter_names.json')
    jar.manifest.attributes(getManifestAttributes("Mekanism"))
}

tasks.named('sourcesJar', Jar).configure { Jar jar ->
    dependsOn(classes, apiClasses, additionsClasses, defenseClasses, generatorsClasses, toolsClasses)
    jar.duplicatesStrategy(DuplicatesStrategy.FAIL)
    //Note: Already contains main source set's sources by default
    jar.from(sourceSets.api.allJava, sourceSets.additions.allJava, sourceSets.defense.allJava, sourceSets.generators.allJava, sourceSets.tools.allJava)
    jar.manifest.attributes(getManifestAttributes("Mekanism"))
}

def secondaryJar(SourceSet sourceSet, String title) {
    return tasks.register(sourceSet.getJarTaskName(), Jar, {
        duplicatesStrategy(DuplicatesStrategy.FAIL)
        archiveClassifier.set(sourceSet.name)
        from sourceSet.output
        if (!title.isEmpty()) {
            archiveFileName.set("${title}-${project.version}.jar")
        }
        manifest.attributes(getManifestAttributes(title.isEmpty() ? "Mekanism" : title))
    })
}

def apiJar = secondaryJar(sourceSets.api, '')
def additionsJar = secondaryJar(sourceSets.additions, 'MekanismAdditions')
def defenseJar = secondaryJar(sourceSets.defense, 'MekanismDefense')
def generatorsJar = secondaryJar(sourceSets.generators, 'MekanismGenerators')
def toolsJar = secondaryJar(sourceSets.tools, 'MekanismTools')

def allJar = tasks.register('allJar', Jar, {
    dependsOn(classes, apiClasses, additionsClasses, defenseClasses, generatorsClasses, toolsClasses)
    mustRunAfter(clean)
    duplicatesStrategy(DuplicatesStrategy.FAIL)
    archiveClassifier.set("all")
    manifest.attributes(getManifestAttributes("MekanismAll"))
    def combineSets = [sourceSets.main, sourceSets.additions, sourceSets.defense, sourceSets.generators, sourceSets.tools]
    doFirst {
        //Start by generating the merged data
        MergeJars.merge(project, combineSets)
    }
    //Then copy all the files except for ones we are going to include from the merged
    from(sourceSets.api.output)
    List<String> baseExcludeData = MergeJars.getGeneralPathsToExclude(project, combineSets)
    Closure excludeFromMain = MergeJars.createExcludeClosure(baseExcludeData, 'crafttweaker_parameter_names.json')
    from(sourceSets.main.output, excludeFromMain)
    Closure excludeFromAll = MergeJars.createExcludeClosure(baseExcludeData, 'logo.png', 'pack.mcmeta')
    for (String name : secondaryModules) {
        from(sourceSets.getByName(name).output, excludeFromAll)
    }
    //And finally copy over the generated files
    MergeJars.getGeneratedClosures(versionProperties).each { closure -> from(layout.buildDirectory.dir("generated"), closure) }
})

tasks.withType(JavaCompile).configureEach({
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(["-Xmaxerrs", "100000"])
})

tasks.withType(Javadoc).configureEach({
    options.tags = [
            'apiNote:a:<em>API Note:</em>',
            'implSpec:a:<em>Implementation Requirements:</em>',
            'implNote:a:<em>Implementation Note:</em>'
    ]
})

artifacts {
    archives apiJar
    archives jar
    archives additionsJar
    archives generatorsJar
    archives defenseJar
    archives toolsJar
    archives allJar
    archives sourcesJar
}

//Minimize/optimize all png files, requires optipng on the PATH
// Credits: BrainStone
void minimizePNGFile(File file) {
    long size = file.length()
    exec {
        executable "optipng"
        args "-q", "-o7", "-zm1-9", "-strip", "all", file
    }
    long newSize = file.length()
    if (newSize < size) {
        System.out.format("Reduced File size of %s from %d bytes to %d bytes (reduced by %.2f%%)\n",
                file, size, newSize, ((double) (size - newSize)) / ((double) size) * 100.0)
    }
}

tasks.register('optimizePng').configure {
    doLast {
        def pngPatterns = ["**/*.png"]
        //Ensure the logo is minimized (we add this file to each jar)
        minimizePNGFile(file("${projectDir}/logo.png"))
        //Minimize any PNGs in the source sets
        def sourceSets = [sourceSets.main, sourceSets.additions, sourceSets.generators, sourceSets.defense, sourceSets.tools]
        for (def sourceSet : sourceSets) {
            for (dir in sourceSet.resources.srcDirs) {
                fileTree(dir: dir, includes: pngPatterns).each { minimizePNGFile(it) }
            }
        }
    }
}

//properties to put in $HOME/.gradle/gradle.properties
def MAVEN_PASSWORD_PROPERTY_NAME = 'mekanismMavenPassword'
def MODMAVEN_PW = System.getenv('MEK_MAVEN_PW') ?: (String) project.findProperty(MAVEN_PASSWORD_PROPERTY_NAME)

publishing {
    publications { PublicationContainer publicationContainer ->
        publicationContainer.register("maven", MavenPublication) { MavenPublication publication ->
            publication.from((SoftwareComponent) components.java)
            publication.groupId = project.group
            publication.version = project.version
            publication.artifactId = System.getenv("MAVEN_ARTIFACT") ?: project.base.archivesName.get()
            publication.artifacts = [apiJar, jar, generatorsJar, additionsJar, toolsJar, allJar, sourcesJar]
            publication.pom {
                name.set("Mekanism")
                packaging = 'jar'
                description.set('Mekanism is a Minecraft add-on featuring high-tech machinery that can be used to create powerful tools, armor, and weapons.')
                url.set('https://aidancbrady.com/mekanism/')
                scm {
                    url.set('https://github.com/mekanism/Mekanism.git')
                }
                issueManagement {
                    system.set('github')
                    url.set('https://github.com/mekanism/Mekanism/issues')
                }
                licenses {
                    license {
                        name.set('MIT')
                        distribution.set('repo')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://modmaven.dev/artifactory/local-releases/'
            if (MODMAVEN_PW != null) {
                credentials {
                    username = "mekanism"
                    password = MODMAVEN_PW
                }
            }
        }
    }
}

def resolvedChangelog = null

//closure to generate the changelog once, and only when needed by CurseGradle or Modrinth
def changeLogResolver = { ->
    if (resolvedChangelog != null) {
        println "Using cached changelog"
        return resolvedChangelog
    }
    def generatedChangelog = "Unable to generate changelog :("
    def currentCommit = System.getenv("GIT_COMMIT")
    def prevCommit = System.getenv("GIT_PREVIOUS_SUCCESSFUL_COMMIT") ?: System.getenv("GIT_PREVIOUS_COMMIT")

    if (currentCommit != null && prevCommit != null) {
        generatedChangelog = ""
        //Use the service to avoid eagerly instantiating the grgit instance, and only do so when we actually need it
        // for usage in generating the changelog for either CF or Modrinth
        grgitService.service.get().grgit.log {
            range(prevCommit, currentCommit)
        }.reverse().each { commit ->
            //Use full message rather than short message to get any new lines, and trim it so that any trailing new lines
            // get removed so that we don't end up with extra spaces
            String message = commit.fullMessage.trim()
            if (!message.startsWith("Merge branch") && !message.startsWith("Merge pull request") && !message.contains("[no-changelog]")) {
                //Ignore Merges and PR Merges
                message = message.replaceAll("#(\\d+)", { match ->//turn issues/prs into links (github currently supports prs being linked as issues)
                    return "[${match[0]}](https://github.com/mekanism/Mekanism/issues/${match[1]})"
                }).replaceAll("\\n", "  \n\t")//convert new lines that are part of a commit message into new lines and a tab
                if (generatedChangelog != "") {
                    //If this isn't the first commit prepend an extra newline
                    generatedChangelog += "  \n"
                }
                generatedChangelog += "[${commit.getAbbreviatedId()}](https://github.com/mekanism/Mekanism/commit/${commit.id}) - ${message}"
            }
        }
        println "Changelog generated"
    }

    def releaseNotesFile = project.file("docs/release_${mod_version}.md")
    if (releaseNotesFile.exists()) {
        //Add any version specific changelog stuff
        def releaseNotes = releaseNotesFile.getText()
        generatedChangelog = "$releaseNotes\n\n $generatedChangelog"
    }

    if (release_type == "alpha") {
        //Add a warning at the top about what an alpha build means
        generatedChangelog = "Warning: Mekanism is currently in alpha, and is not recommended for widespread use in modpacks. There are likely to be game breaking bugs, " +
                "and updating from one alpha to the next may cause various mekanism blocks to disappear/void their contents. While we will try to not have this happen/keep " +
                "it to a minimum make sure to make backups. You can read more about the alpha state of this project [here](https://github.com/mekanism/Mekanism#alpha-status)." +
                "<br> $generatedChangelog"
    }
    resolvedChangelog = generatedChangelog
    return generatedChangelog
}

tasks.register('outputChangelog') {
    doLast {
        project.file("build/changelog.md").text = changeLogResolver.call()
    }
}

def mainCfUpload, additionsCfUpload, generatorsCfUpload, toolsCfUpload;

if (System.getenv("CURSEFORGE_KEY") != null || project.hasProperty('curseforgeKey')) {
    println "Enabling Curseforge config"
    tasks.register("curseforge", TaskPublishCurseForge, { task ->
        setGroup("publishing")
        setDescription("Upload Mekanism to CurseForge")

        apiToken = System.getenv("CURSEFORGE_KEY") ?: project.findProperty("curseforgeKey")

        def changelog = changeLogResolver.call()

        //Main Mekanism Project
        mainCfUpload = task.upload(268560, jar) { main ->
            setGenericCurseArtifactData(main, changelog)
            //Include the API jar as a secondary file to the main file
            withAdditionalFile(apiJar)
            //Add optional deps
            addOptional(
                    //Computers
                    'cc-tweaked', 'oc2',
                    //Looking at mods
                    'jade', 'the-one-probe', 'wthit-forge',
                    //Recipe viewers
                    'emi', 'jei',
                    //Tweakers
                    'crafttweaker', 'jeitweaker',
                    //World gen
                    'biomes-o-plenty',
                    //Misc
                    'applied-energistics-2', 'ctm', 'curios', 'dark-mode-everywhere', 'farmers-delight', 'female-gender-forge', 'flux-networks',
                    'json-things', 'perviaminvenire', 'projecte', 'recipe-stages'
            )
        }

        //Secondary modules/projects
        additionsCfUpload = uploadSecondaryCurseProject(task, 345425, changelog, additionsJar)
        //uploadSecondaryCurseProject(task, 376939, changelog, defenseJar)
        generatorsCfUpload = uploadSecondaryCurseProject(task, 268566, changelog, generatorsJar)
        toolsCfUpload = uploadSecondaryCurseProject(task, 268567, changelog, toolsJar)

        doLast {
            println("https://www.curseforge.com/minecraft/mc-mods/mekanism/files/${mainCfUpload.curseFileId}")
            println("https://www.curseforge.com/minecraft/mc-mods/mekanism-additions/files/${additionsCfUpload.curseFileId}")
            println("https://www.curseforge.com/minecraft/mc-mods/mekanism-generators/files/${generatorsCfUpload.curseFileId}")
            println("https://www.curseforge.com/minecraft/mc-mods/mekanism-tools/files/${toolsCfUpload.curseFileId}")
        }
    })
}

void setGenericCurseArtifactData(UploadArtifact artifact, String changelog) {
    artifact.changelog = changelog
    artifact.changelogType = 'markdown'
    artifact.releaseType = "${release_type}"
}

UploadArtifact uploadSecondaryCurseProject(TaskPublishCurseForge task, long projectId, String changelog, TaskProvider<Jar> sourceSetJar) {
    return task.upload(projectId, sourceSetJar) { artifact ->
        setGenericCurseArtifactData(artifact, changelog)
        addRequirement('mekanism')
    }
}

if (System.getenv("MODRINTH_TOKEN") != null || project.hasProperty('modrinthToken')) {
    println "Enabling Modrinth config"
    def additionsModrinth = createSecondaryModrinthUpload("additions", "a6F3uASn", additionsJar)
    //def defenseModrinth = createSecondaryModrinthUpload("defense", "", defenseJar)
    def generatorsModrinth = createSecondaryModrinthUpload("generators", "OFVYKsAk", generatorsJar)
    def toolsModrinth = createSecondaryModrinthUpload("tools", "tqQpq1lt", toolsJar)
    tasks.named('modrinth').configure {
        dependsOn(jar, apiJar)
        finalizedBy(additionsModrinth, /*defenseModrinth,*/ generatorsModrinth, toolsModrinth)
    }

    modrinth {
        if (System.getenv("MODRINTH_TOKEN") == null) {
            //Defaults to MODRINTH_TOKEN so we only have to check for a token via gradle properties if there isn't a system property
            getToken().value((String) project.findProperty("modrinthToken"))
        } else {
            token = System.getenv("MODRINTH_TOKEN")
        }
        projectId = "Ce6I4WUE"
        //Use the full "basic" version number rather than just mod version number so that if multiple releases end up happening due to bugs
        // then we don't run into conflicts in uploading
        //Note: The versionName is set to the versionNumber automatically
        versionNumber = "${basicVersion}"
        versionType = "${release_type}"
        gameVersions.add("${minecraft_version}")
        loaders.add("neoforge")
        failSilently = true
        changelog = changeLogResolver.call()
        uploadFile = jar

        getAdditionalFiles().value([apiJar.get()])
        //Note: Can't use nested dependency configuration as we have no way to clear it for the secondary uploads
        getDependencies().value([
                new ModDependency("XxWD5pD3", "optional"),//AE2
                new ModDependency("gu7yAYhd", "optional"),//CC: Tweaked
                new ModDependency("Xg35A4rS", "optional"),//CraftTweaker
                new ModDependency("vvuO3ImH", "optional"),//Curios
                new ModDependency("fRiHVvU7", "optional"),//EMI
                new ModDependency("R2OftAxM", "optional"),//Farmer's Delight
                new ModDependency("nvQzSEkH", "optional"),//Jade
                new ModDependency("u6dRKJwZ", "optional"),//JEI
                new ModDependency("EiEOyeoL", "optional"),//JEI Tweaker
                new ModDependency("uE1WpIAk", "optional"),//Oh the Biomes You'll Go
                new ModDependency("qXkLFB0L", "optional"),//Recipe Stages
                new ModDependency("Eyw0UxEx", "optional"),//The One Probe
                new ModDependency("bcOsQHnC", "optional"),//Wildfire's Female Gender Mod
                new ModDependency("6AQIaxuO", "optional"),//WTHIT
        ])
    }

}

def createSecondaryModrinthUpload(String output, String targetProjectId, TaskProvider<Jar> sourceSetJar) {
    return tasks.register("${output}Modrinth", TaskModrinthUpload, {
        setGroup("publishing")
        setDescription("Upload Mekanism ${output} to Modrinth")
        dependsOn(tasks.named("modrinth"), sourceSetJar)
        doFirst {
            //Run in do first to change the configured extension values before it starts applying
            modrinth {
                projectId = targetProjectId
                uploadFile = sourceSetJar.get()
                //Clear additional upload files as this is a modification of what things are set to from before
                getAdditionalFiles().empty()
                //Mark that the addon requires the build of Mekanism that we just uploaded (and override it to clear all optional dependencies)
                if (tasks.modrinth.newVersion == null || tasks.modrinth.newVersion.getId() == null) {
                    //If we failed to find the upload version just set it to Mekanism in general
                    getDependencies().value([new ModDependency("Ce6I4WUE", "required")])
                } else {
                    getDependencies().value([new VersionDependency(tasks.modrinth.newVersion.getId(), "required")])
                }
            }
            println "Updated Modrinth extension"
        }
    })
}
